plugins {
    id 'java-library'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
}

group = 'world.landfall'
version = '1.2.2'

def targetJavaVersion = 17

repositories {
    mavenCentral()
}

dependencies {
    // Pure Java dependencies only â€” no platform APIs
    api 'com.zaxxer:HikariCP:5.1.0'
    api 'com.google.code.gson:gson:2.10.1'
    api "mysql:mysql-connector-java:8.0.33"
    api 'net.dv8tion:JDA:5.0.0-beta.8'
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api 'org.slf4j:slf4j-api:2.0.9'
}

java {
    sourceCompatibility = targetJavaVersion
    targetCompatibility = targetJavaVersion
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = ['version': project.version]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })
project.idea.project.settings.taskTriggers.afterSync generateTemplates
